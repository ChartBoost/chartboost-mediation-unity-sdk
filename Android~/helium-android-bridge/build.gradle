plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdkVersion = 32
    buildToolsVersion = '32.0.0'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 32
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

signing {
   required {
       isPublicRelease && gradle.taskGraph.hasTask("publish")
   }
    useGpgCmd()
    sign publishing.publications
}

tasks.withType(Sign) {
   onlyIf {isPublicRelease}
}

task javaDoc(type: Jar) {
    from android.sourceSets.release.java.srcDirs
    classifier = 'javadoc'
}

task javaSources(type: Jar) {
    from android.sourceSets.release.java.srcDirs
    classifier = 'sources'
}

repositories {
    google()
    mavenCentral()
    flatDir {
        dirs 'helium-sdk-aar'
    }
}

ext {
    androidPluginFolder = "$rootDir/../Runtime/Plugins/Android"
}

dependencies {
    //Make sure to bump the version on new releases.
    api fileTree(dir: 'libs', include: ['*.jar'])
    implementation(name: 'Helium-2.10.0-release', ext: 'aar')
    implementation 'androidx.annotation:annotation:1.4.0'
}

task assembleForPlugin(type: Jar, dependsOn: 'bundleLibRuntimeToJarRelease') {
    // We are only interested in the HeliumUnityBridge classes only.
    from zipTree("$projectDir/build/intermediates/runtime_library_classes_jar/release/classes.jar")
    include '**/*.class'
    exclude 'META-INF/'
    exclude 'META-INF/MANIFEST.MF'
}

task copyReleaseAndroidBridgeForPackaging(type: Copy, dependsOn: 'assembleForPlugin') {
    from ("$projectDir/build/intermediates/runtime_library_classes_jar/release/classes.jar")
    into (androidPluginFolder)
    rename("classes.jar", "helium-android-bridge.jar")
}
