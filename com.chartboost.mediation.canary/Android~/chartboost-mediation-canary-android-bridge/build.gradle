plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdkVersion = 33
    buildToolsVersion = '33.0.0'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
    }
    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

repositories {
    google()
    mavenCentral()
    maven {
        url "https://cboost.jfrog.io/artifactory/chartboost-mediation/"
    }
    maven {
        url "https://cboost.jfrog.io/artifactory/private-chartboost-mediation/"
        credentials {
            username privateUser
            password privatePass
        }
    }
}

ext {

    chartboostMediationDependency = "com.chartboost:chartboost-mediation-sdk:$chartboostMediationVersion"
    androidPluginFolder = "$rootDir/../Assets/Plugins/Android/ChartboostMediationCanary.plugin/libs"
    print("Attempting to Compile Unity Canary Android Bridge with Chartboost Mediation SDK version: $chartboostMediationDependency")
}

dependencies {
    //Make sure to bump the version on new releases.
    api fileTree(dir: 'libs', include: ['*.jar'])
    implementation chartboostMediationDependency
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    implementation 'com.amazon.android:aps-sdk:9.6.2'
    implementation 'com.facebook.android:audience-network-sdk:6.12.0'
    implementation 'com.applovin:applovin-sdk:11.5.5'

    // QR code scanner dependencies
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
}

task assembleForPlugin(type: Jar, dependsOn: 'bundleLibRuntimeToJarRelease') {
    dependsOn 'androidDependencies'
    // We are only interested in the Canary Bridge classes only.
    from zipTree("$projectDir/build/intermediates/runtime_library_classes_jar/release/classes.jar")
    include '**/*.class'
    exclude 'META-INF/'
    exclude 'META-INF/MANIFEST.MF'
}

task copyReleaseAndroidBridgeForPackaging(type: Copy, dependsOn: 'assembleForPlugin') {
    from ("$projectDir/build/intermediates/runtime_library_classes_jar/release/classes.jar")
    into (androidPluginFolder)
    rename("classes.jar", "chartboost-mediation-unity-canary-android-bridge.jar")
}
