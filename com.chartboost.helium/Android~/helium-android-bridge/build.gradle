plugins {
    id 'com.android.library'
    id 'signing'
}

android {
    compileSdkVersion = 32
    buildToolsVersion = '32.0.0'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

repositories {
    google()
    mavenCentral()
    maven {
        url "https://cboost.jfrog.io/artifactory/helium"
    }
    maven {
        url "https://cboost.jfrog.io/artifactory/private-helium/"
        credentials {
            username System.getenv("JFROG_USER")
            password System.getenv("JFROG_PASS")
        }
    }
}

ext {
    // use whatever is the latest in any of the repos
    heliumVersion = '+'
    if (versionInput != null)
        heliumVersion = versionInput

    heliumDependency = "com.chartboost:helium:$heliumVersion"

    print("Attempting to Compile Android Bridge with Helium SDK version: $heliumDependency")
}

dependencies {
    //Make sure to bump the version on new releases.
    api fileTree(dir: 'libs', include: ['*.jar'])
    implementation heliumDependency
    implementation 'androidx.annotation:annotation:1.4.0'
    implementation 'org.greenrobot:eventbus:3.3.1'
}

task assembleForPlugin(type: Jar, dependsOn: 'bundleLibRuntimeToJarRelease') {
    dependsOn 'androidDependencies'
    // We are only interested in the HeliumUnityBridge classes only.
    from zipTree("$projectDir/build/intermediates/runtime_library_classes_jar/release/classes.jar")
    include '**/*.class'
    exclude 'META-INF/'
    exclude 'META-INF/MANIFEST.MF'
}

task copyReleaseAndroidBridgeForPackaging(type: Copy, dependsOn: 'assembleForPlugin') {
    from ("$projectDir/build/intermediates/runtime_library_classes_jar/release/classes.jar")
    into ("$rootDir/../Runtime/Plugins/Android")
    rename("classes.jar", "helium-android-bridge.jar")
}
